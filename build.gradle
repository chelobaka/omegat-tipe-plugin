plugins {
    id 'java'
    id "com.github.spotbugs" version "3.0.0"
    id 'checkstyle'
    id 'distribution'
    id 'maven'
}

def pluginName = 'TipeÂ³ format support'
def pluginAuthor = 'Lev Abashkin'

// Project version
version = '0.5.1'

repositories {
    maven {
        url "https://dl.bintray.com/omegat-org/maven"
    }
    jcenter()
}

def compileDependencies = [
        'org.omegat:omegat:4.3.0',
        'commons-io:commons-io:2.4',
        'commons-lang:commons-lang:2.6'
]

def testDependencies = [
        'junit:junit:4.12',
        'xmlunit:xmlunit:1.6',
        'org.madlonkay.supertmxmerge:supertmxmerge:2.0.1'
]

dependencies {

    compileDependencies.each {
        compileOnly it
        testImplementation it
    }

    testDependencies.each {
        testImplementation it
    }
}


tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

jar {
    manifest {
        // plugin's main class name is defined in gradle.properties file.
        attributes(
                "OmegaT-Plugins": pluginMainClass,
                "Plugin-Version": archiveVersion,
                "Plugin-Name": pluginName,
                "Plugin-Author": pluginAuthor
        )
    }
}

/* Checkstyle */
tasks.checkstyleMain.setGroup('Verification')
tasks.checkstyleTest.setEnabled(false)

/* SpotBugs */
spotbugs {
    ignoreFailures = true
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    setGroup('Verification')
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
tasks.spotbugsTest.setEnabled(false)

artifacts {
    archives jar
}

distTar {
  compression = Compression.GZIP
}

distributions {
    main {
        contents {
            from('README.md', 'COPYING')
            into('docs') {
                from 'CHANGELOG.md'
            }

            from(jar) {
                into 'plugin'
            }
        }
    }
}